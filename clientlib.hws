; Connect to the specified Server and Port number
Function p_ConnectToServer(server$, port$)
	If server$ = "" Then server$ = "127.0.0.1"
	If port$ = "" Then port$ = 2550
	p_Log("Attempting to connect to server " .. server$ .. ", port " .. port$ .. "...")
	; Open a connection and get the Id in a variable
	connectionId = OpenConnection(Nil, server$, port$)
	p_Log("Connected!")
	; Add connectionId to list of active Connections
	p_AddConnection(connectionId)
	; Change UI state
	p_SetUIState("connected")
	; temporary, this should check if the nick is in use on the server already
	nick$ = moai.Get("Nick", "Text")
EndFunction

; Disconnect from the currently connected Server
Function p_DisconnectFromServer()
	CloseConnection(connectionId)
	p_RemoveConnection(connectionId)
	p_SetUIState("disconnected")
EndFunction

; Handle received data from a connected client
Function p_ReceiveData(msg)
	Local data$ = ReceiveData(msg.id, #RECEIVELINE)
	p_Log("Message received: " .. data$)
	p_ProcessMessagesByType(data$)
EndFunction

; Disable or enable UI elements based on current state
Function p_SetUIState(state$)
	If state$ = "connected"
		moai.Set("CmdDir", "disabled", True)
		moai.Set("ProjectsDir", "disabled", True)
		moai.Set("txtServer", "disabled", True)
		moai.Set("txtPort", "disabled", True)
		moai.Set("connect","disabled", True)
		moai.Set("Nick", "disabled", True)
		moai.Set("AltNick", "disabled", True)
		moai.Set("disconnect","disabled", False)
		
	ElseIf state$ = "disconnected"
		moai.Set("CmdDir", "disabled", False)
		moai.Set("ProjectsDir", "disabled", False)
		moai.Set("txtServer", "disabled", False)
		moai.Set("txtPort", "disabled", False)
		moai.Set("connect","disabled", False)
		moai.Set("Nick", "disabled", False)
		moai.Set("AltNick", "disabled", False)
		moai.Set("disconnect","disabled", True)
		
	EndIf
EndFunction

Function p_ApplySettings()
	server$ = moai.Get("txtServer", "Text")
	port$ = moai.Get("txtPort", "Text")
	nick$ = moai.Get("Nick", "Text")
	lwsn$ = moai.Get("LWSN", "File")
	commandDir$ = moai.Get("CmdDir", "Path")
EndFunction

Function p_ReadSettingsFile(settingsLine$)
	If StartsWith(settingsLine$, "Server=")
		server$ = UnrightStr(settingsLine$, 7)
		moai.Set("txtServer", "Text", server$)
	ElseIf StartsWith(settingsLine$, "Port=")
		port$ = UnrightStr(settingsLine$, 5)
		moai.Set("txtPort", "Text",port$)
	ElseIf StartsWith(settingsLine$, "Nickname=")
		nick$ = UnrightStr(settingsLine$, 9)
		moai.Set("Nick", "Text",nick$)
	ElseIf StartsWith(settingsLine$, "LWSN=")
		lwsn$ = UnrightStr(settingsLine$, 5)
		moai.Set("LWSN", "File", lwsn$)
	ElseIf StartsWith(settingsLine$, "CommandDir=")
		commandDir$ = UnrightStr(settingsLine$, 11)
		moai.Set("CmdDir", "Path", commandDir$)
	EndIf
EndFunction

Function p_SaveSettings()
	OpenFile(2, "client_settings.ini", #MODE_WRITE)
	WriteLine(2, "Server=" .. server$)
	WriteLine(2, "Port=" .. port$)
	WriteLine(2, "Nickname=" .. nick$)
	WriteLine(2, "LWSN=" .. lwsn$)
	WriteLine(2, "CommandDir=" .. commandDir$)
	CloseFile(2)
EndFunction